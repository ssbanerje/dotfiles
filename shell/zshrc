#!/usr/bin/env zsh

# ZSH Options {{{

HISTSIZE=10000
SAVEHIST=10000
setopt AUTOCD
setopt ALWAYS_TO_END COMPLETE_IN_WORD
setopt AUTOPUSHD PUSHDMINUS PUSHDSILENT PUSHDTOHOME PUSHDIGNOREDUPS
setopt CORRECT
setopt COMPLETE_ALIASES COMPLETE_IN_WORD
setopt EXTENDED_GLOB
setopt HIST_EXPIRE_DUPS_FIRST HIST_FIND_NO_DUPS HIST_IGNORE_ALL_DUPS HIST_REDUCE_BLANKS SHARE_HISTORY
setopt LOCAL_OPTIONS LOCAL_TRAPS

# }}}

# Setup environment {{{

# Environement config
source "$HOME/.config/env.sh"

# Prompt config
source "$HOME/.p10k.zsh"

# Setup Homebrew FPATH
if [[ "${UNAME:=$(uname -s)}" == "Darwin" ]]; then
  if (( ! ${fpath[(I)/usr/local/share/zsh/site-functions]} )); then
    fpath+=("${BREW_PREFIX:=$(brew --prefix)}/share/zsh/site-functions")
  fi
fi

# }}}

# Skip the remaining file for non-interactive shells
if [[ "$-" != *i* ]]; then
  return
fi

# Load Plugins {{{

# Disable P10K prompt
export POWERLEVEL9K_DISABLE_CONFIGURATION_WIZARD=true

# Load antigen + plugins
source "$HOME/.config/antigen.zsh"
antigen init "$HOME/.config/antigenrc"

# Make fancy-ctrl-z work with zvm
zvm_define_widget fancy-ctrl-z fancy-ctrl-z
zvm_bindkey viins '^Z' fancy-ctrl-z

# }}}

# Deferred Loading Helper {{{

function CHECK_AND_SOURCE_DEFERRED() {
  while [[ "$#" -ne 0 ]]; do
    if [[ -r "$1" ]]; then
      if [[ "$2" == deferarg* ]]; then
        zsh-defer "$(echo $2 | sed 's/^deferarg(\(.*\))$/\1/')" source "$1"
        shift
      else
        zsh-defer source "$1"
      fi
    fi
    shift
  done
}

# }}}

# Completion and Keybindings {{{

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'
zstyle ':completion:*:*:*:*:processes' command "ps -u $(whoami) -o pid,user,comm -w -w"
zstyle ':completion:*:ssh:*' tag-order hosts users
zstyle ':completion:*:ssh:*' group-order hosts-domain hosts-host users hosts-ipaddr
zstyle ':completion:*:functions' ignored-patterns '_*'
zstyle ':completion:*:*:kill:*' menu yes select

if [[ "${UNAME:=$(uname -s)}" == "Linux" ]]; then
  CHECK_AND_SOURCE_DEFERRED \
    /etc/grc.zsh \
    /usr/share/doc/fzf/examples/completion.zsh \
    /usr/share/doc/fzf/examples/key-bindings.zsh
elif [[ "${UNAME:=$(uname -s)}" == "Darwin" ]]; then
  CHECK_AND_SOURCE_DEFERRED \
    $([[ "$TERM_PROGRAM" == "iTerm.app" ]] && echo "$HOME/.config/iterm2_shell_integration.zsh") \
    "${BREW_PREFIX:=$(brew --prefix)}/etc/grc.zsh" \
    "${BREW_PREFIX:=$(brew --prefix)}/opt/fzf/shell/completion.zsh" \
    "${BREW_PREFIX:=$(brew --prefix)}/opt/fzf/shell/key-bindings.zsh"
fi

# }}}

# Load aliases and colors
CHECK_AND_SOURCE_DEFERRED \
  "$HOME/.config/aliases.sh" \
  "$HOME/.config/base16-shell/scripts/base16-material.sh" "deferarg(-1)"

unset CHECK_AND_SOURCE_DEFERRED

# vim:foldmethod=marker
